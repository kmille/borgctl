# Maintainer: kmille github@androidloves.me

pkgname=borgctl
pkgver=0.4.10.r0.g1db74f1
pkgrel=1
pkgdesc="borgctl - borgbackup without writing bash scripts"
arch=('any')
url="https://github.com/kmille/borgctl"
license=('GPL3')
makedepends=(python-poetry-core python-build python-installer python-wheel)
depends=(python python-ruamel-yaml)
#source=("$pkgname"::"git+file:///home/kmille/projects/borgctl")
_source="$(realpath "$(pwd)/../../")"
source=("$pkgname"::"git+file://$_source")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}


build() {
  cd "$pkgname"
  # Note: set `GIT_CEILING_DIRECTORIES` to prevent poetry
  # from incorrectly using a parent git checkout info.
  # https://github.com/pypa/build/issues/384#issuecomment-947675975
  GIT_CEILING_DIRECTORIES="${PWD}/.." python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}

